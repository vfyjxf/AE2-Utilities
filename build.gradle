buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = 'https://maven.minecraftforge.net'
        }
        maven {
            name = "github"
            url = "https://github.com/juanmuscaria/maven/raw/master"
        }
        maven {
            name = "sponge"
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = mod_version
group = mod_group
archivesBaseName = mod_archives_name

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    version = "1.12.2-14.23.5.2847"
    mappings = "stable_39"
    runDir = "run"

    replace '@VERSION@', project.version
    // makeObfSourceJar = false
    clientJvmArgs += "-Dmixin.debug.export=true"
    serverJvmArgs += "-Dmixin.debug.export=true"
}

processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version,
                'mod_id': mod_id,
                'mod_name': mod_name,
                'mod_author': mod_author,
                'mod_description': mod_description,
                'mc_version': mc_version,
                'mod_version': mod_version,
                'mod_icon': mod_icon
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }
    maven {
        name = "sponge"
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {

    deobfCompile "mezz.jei:jei_${mc_version}:${jei_version}"
    deobfCompile "curse.maven:applied-energistics-2-223794:2747063"
    deobfCompile "curse.maven:mixin-booter-419286:3321174"

    compile("org.spongepowered:mixin:0.8.2") {
        exclude module: "asm-commons"
        exclude module: "asm-tree"
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "log4j-core"
        exclude module: "gson"
        exclude module: "commons-io"
    }
}


task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

sourceSets {
    main {
        ext.refMap = "mixins.ae2utilities.refmap.json"
    }
}

jar {
    appendix project.minecraft.version
}

artifacts {
    archives devJar
    archives sourcesJar
}